skycity,115,213,6	script	Safe Refiner	731,{
	disable_items;
	.@npc_name$ = "Safe Refiner";

	mes "["+ .@npc_name$ +"]";
	mes "Hello, I can Safe Refine any of your equipment using expensive materials. Safe Refining requires additional copies of the item you are refining and 1,000,000,000z.";
	mes "^ff4444The materials used on this process must not have any cards on it and no refinement level.^000000";
	next;
	mes "I can refine all kinds of weapons, armor and equipment, so let me know what you want me to refine.";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(.@i = 1; .@i<getarraysize(.@indices); ++.@i) {
		if(getequipisequiped(.@indices[.@i])) {
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			.@equipped = 1;
		}
		.@menu$ = .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "["+ .@npc_name$ +"]";
		mes "I don't think I can refine any items you have...";
		close;
	}
	.@part = .@indices[select(.@menu$)];

	if(!getequipisequiped(.@part)) { //custom check
		mes "["+ .@npc_name$ +"]";
		mes "You're not wearing";
		mes "anything there that";
		mes "I can refine.";
		emotion ET_FRET;
		close;
	}
	//Check if the item is refinable...
	if(!getequipisenableref(.@part)) {
		mes "["+ .@npc_name$ +"]";
		mes "I don't think I can";
		mes "refine this item at all...";
		close;
	}
	//Check to see if the items is already +10
	if(getequiprefinerycnt(.@part) >= 10) {
		mes "["+ .@npc_name$ +"]";
		mes "I can't refine this";
		mes "any more. This is as";
		mes "refined as it gets!";
		close;
	}
	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	.@price = 1000000000;

	switch (getequipweaponlv(.@part)) {
		case 1: .@startref = 7; break;
		case 2: .@startref = 6; break;
		case 3: .@startref = 5; break;
		case 4:
		default: .@startref = 4; break;
	}
	if (.@refinerycnt < .@startref) {
		mes "["+ .@npc_name$ +"]";
		mes "I can only refine items that cannot be safely refine by Hollgrehenn.";
		close;
	}

	mes "["+ .@npc_name$ +"]";
	mes "To refine this I need";
	mes (.@refinerycnt+1)+" ^003366"+getitemname(.@refineitemid)+"^000000 and";
	mes "a service fee of " + .@price + " Zeny.";
	mes "Do you really wish to continue?";
	next;
	if(select("Yes:No") == 2){
		mes "["+ .@npc_name$ +"]";
		mes "Yeah...";
		mes "There's no need to";
		mes "rush. Take your time.";
		close;
	}
	set .@matcount,0;
	getinventorylist;
	for (.@i = 0; .@i < @inventorylist_count; ++.@i) {
		if (@inventorylist_id[.@i] == .@refineitemid && !@inventorylist_equip[.@i] && @inventorylist_card1[.@i] == 0 && @inventorylist_refine[.@i] == 0) {
			.@matcount++;
		}
		if (.@matcount == .@refinerycnt+1) break;
	}
	if(.@matcount < .@refinerycnt+1 || Zeny < .@price) {
		mes "["+ .@npc_name$ +"]";
		mes "You don't seem to have enough Zeny or "+getitemname(.@refineitemid)+"...";
		mes "Go get some more or check if there is any material with card or has a refinement level. I'll be here all day if you need me.";
		close;
	}
	Zeny = Zeny-.@price;
	set .@delcount,0;
	for (.@i = 0; .@i < @inventorylist_count; ++.@i) {
		if (.@delcount < .@refinerycnt+1 && @inventorylist_id[.@i] == .@refineitemid && !@inventorylist_equip[.@i] && @inventorylist_card1[.@i] == 0 && @inventorylist_refine[.@i] == 0) {
			delitem @inventorylist_id[.@i], 1;
			set .@delcount,.@delcount+1;
		}
	}

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) ||
	    callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt)) {
		mes "["+ .@npc_name$ +"]";
		emotion ET_FRET;
		mes "Wait a second...";
		mes "Do you think I'm stupid?!";
		mes "You switched the item while I wasn't looking! Get out of here!";
		close;
	}
	mes "["+ .@npc_name$ +"]";
	successrefitem .@part;
	emotion ET_SMILE;
	.@win = rand(1,3);
	if (.@win == 1) {
		mes "Perfect!";
		mes "Heh heh!";
		mes "Once again,";
		mes "flawless work";
		mes "from the master~";
	} else if(.@win == 2) {
		mes "Success...!";
		mes "Yet again, my amazing";
		mes "talent truly dazzles";
		mes "and shines today.";
	} else {
		mes "Heh heh!";
		mes "I'm all done.";
		mes "No doubt, my work is";
		mes "to your satisfaction.";
		mes "Sheer, utter perfection~";
	}
	close;
}